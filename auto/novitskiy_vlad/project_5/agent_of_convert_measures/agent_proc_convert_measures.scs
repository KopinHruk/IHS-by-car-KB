agent_scp_program -> agent_proc_convert_measures;;
agent_proc_convert_measures => nrel_main_idtf:
[агентная scp-программа конвертирования величин] (* <- lang_ru;; *);
	[agent scp-program of converting values] (* <- lang_en;; *);;

scp_program -> agent_proc_convert_measures (*
	-> rrel_params: .agent_proc_convert_measures_params (*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
	*);;

	-> rrel_operators: .agent_proc_convert_measures_operator_set (*
		-> rrel_init: .agent_proc_convert_measures_search_question (*
			<- searchElStr3;;
			-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

			=> nrel_goto: .agent_proc_convert_measures_sleep_or_wake_up;;
		*);;

		-> .agent_proc_convert_measures_sleep_or_wake_up (*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_const: question_convert_measures;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_then: .agent_proc_convert_measures_search_input_parameter_of_request;;
			=> nrel_else: .agent_proc_convert_measures_return;;
		*);;

		-> .agent_proc_convert_measures_search_input_parameter_of_request (*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _param;;

			=> nrel_then: .agent_proc_convert_measures_gen_answer_el;;
			=> nrel_else: .agent_proc_convert_measures_return;;
		*);;

		-> .agent_proc_convert_measures_gen_answer_el (*
			<- genEl;;
			-> rrel_1: rrel_assign: rrel_const: rrel_node: rrel_scp_var: _answer;;

			=> nrel_goto: .agent_proc_convert_measures_call_program;;
		*);;

		-> .agent_proc_convert_measures_call_program (*
			<- call;;
			-> rrel_1: rrel_fixed: rrel_scp_const: proc_convert_measures;;
			-> rrel_2: rrel_fixed: rrel_scp_const: .agent_proc_convert_measures_call_program_params (*
				-> rrel_1: rrel_fixed: rrel_scp_var: _param;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _answer;;
			*);;
			-> rrel_3: rrel_assign: rrel_scp_var: _descr;;

			=> nrel_goto: .agent_proc_convert_measures_wait;;
		*);;

		-> .agent_proc_convert_measures_wait (*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

			=> nrel_goto: .agent_proc_convert_measures_connect_answer;;
		*);;

		-> .agent_proc_convert_measures_connect_answer (*
			<- genElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_const: rrel_common: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_answer;;

			=> nrel_goto: .agent_proc_convert_measures_return;;
		*);;

		-> .agent_proc_convert_measures_return (*
			<- return;;
		*);;
	*);;
*);;
