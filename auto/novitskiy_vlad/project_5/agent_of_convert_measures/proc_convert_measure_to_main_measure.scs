scp_program -> proc_convert_measure_to_main_measure (*
	-> rrel_params: .proc_convert_measure_to_main_measure_params (*
        -> rrel_1: rrel_in: _current_first_measure;;
        -> rrel_2: rrel_in: _current_second_measure;;
        -> rrel_3: rrel_in: _current_amount;;
        -> rrel_4: rrel_in: _answer;;
	*);;
	
	-> rrel_operators: ... (*

		-> rrel_init: .proc_convert_measure_to_main_measure_search_measures_arc (*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _current_first_measure;;
			-> rrel_2: rrel_assign: rrel_common: rrel_const: rrel_scp_var: _measures_arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _current_second_measure;;

			=> nrel_then: .proc_convert_measure_to_main_measure_search_current_conversion_rate_number;;
		 	=> nrel_else: .proc_convert_measure_to_main_measure_print_exception_without_conversion_rate;;	
		*);;

        -> .proc_convert_measure_to_main_measure_search_current_conversion_rate_number (*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_common: rrel_const: rrel_scp_var: _measures_arc;;
			-> rrel_2: rrel_assign: rrel_common: rrel_const: rrel_scp_var: _arc2;;
			-> rrel_3: rrel_assign: rrel_scp_var: _current_conversion_rate_number;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_conversion_rate;;

			=> nrel_goto: .proc_convert_measure_to_main_measure_search_current_conversion_rate_number_link;;
		*);;

        -> .proc_convert_measure_to_main_measure_search_current_conversion_rate_number_link (*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _current_conversion_rate_number;;
			-> rrel_2: rrel_assign: rrel_common: rrel_const: rrel_scp_var: _arc2;;
			-> rrel_3: rrel_assign: rrel_link: rrel_scp_var: _current_conversion_rate_number_link;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_main_idtf;;

			=> nrel_goto: .proc_convert_measure_to_main_measure_search_value;;
		*);;

		-> .proc_convert_measure_to_main_measure_search_value (*
			<- searchElStr5;;
			-> rrel_1: rrel_assign: rrel_scp_var: _node1;;
			-> rrel_2: rrel_assign: rrel_common: rrel_const: rrel_scp_var: _arc2;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _current_amount;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_value;;

			=> nrel_goto: .proc_convert_measure_to_main_measure_search_set_of_numbers;;	
		*);;

		-> .proc_convert_measure_to_main_measure_search_set_of_numbers (*
			<- searchSetStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _node1;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
			-> rrel_3: rrel_assign: rrel_scp_var: _curNumber;;

			-> rrel_set_3: rrel_assign: rrel_scp_var: _numberSet;;

			=> nrel_goto: .proc_convert_measure_to_main_measure_next_number;;
		*);;

		-> .proc_convert_measure_to_main_measure_next_number (*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _numberSet;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
			-> rrel_3: rrel_assign: rrel_scp_var: _curNumber;;

			=> nrel_goto: .proc_convert_measure_to_main_measure_erase_number_perm;;
		*);;

		-> .proc_convert_measure_to_main_measure_erase_number_perm (*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_erase: rrel_scp_var: _arc2;;

			=> nrel_goto: .proc_convert_measure_to_main_measure_search_number_current_first_measure;; 
		*);;

		-> .proc_convert_measure_to_main_measure_search_number_current_first_measure (*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_const: number;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _curNumber;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
			-> rrel_5: rrel_fixed: rrel_scp_var: _current_first_measure;;

			=> nrel_then: .proc_convert_measure_to_main_measure_search_current_number_link;;
			=> nrel_else: .proc_convert_measure_to_main_measure_next_number;; 
		*);;

        -> .proc_convert_measure_to_main_measure_search_current_number_link (*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _curNumber;;
			-> rrel_2: rrel_assign: rrel_common: rrel_const: rrel_scp_var: _arc2;;
			-> rrel_3: rrel_assign: rrel_link: rrel_scp_var: _current_number_link;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_main_idtf;;

			=> nrel_then: .proc_convert_measure_to_main_measure_multiply_numbers;;
			=> nrel_else: .proc_convert_measure_to_main_measure_search_current_number_link_system_id;;
		*);;

		-> .proc_convert_measure_to_main_measure_search_current_number_link_system_id (*
			<- searchElStr5;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _curNumber;;
			-> rrel_2: rrel_assign: rrel_common: rrel_const: rrel_scp_var: _arc2;;
			-> rrel_3: rrel_assign: rrel_link: rrel_scp_var: _current_number_link;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc4;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_system_identifier;;

			=> nrel_goto: .proc_convert_measure_to_main_measure_multiply_numbers;;
		*);;

        -> .proc_convert_measure_to_main_measure_multiply_numbers (*
            <- contMult;;
            -> rrel_1: rrel_assign: rrel_scp_var: _current_converted_number_link;;
            -> rrel_2: rrel_fixed: rrel_scp_var: _current_number_link;;
            -> rrel_3: rrel_fixed: rrel_scp_var: _current_conversion_rate_number_link;;

			=> nrel_goto: .proc_convert_measure_to_main_measure_call_proc_generate_answer_construction_output;;
		*);;

		-> .proc_convert_measure_to_main_measure_call_proc_generate_answer_construction_output (*
			<- call;;
			-> rrel_1: rrel_fixed: rrel_scp_const: proc_generate_converting_answer_construction;;
			-> rrel_2: rrel_fixed: rrel_scp_const: .proc_convert_measure_to_main_measure_call_proc_generate_answer_construction_output_params (*
				-> rrel_1: rrel_fixed: rrel_scp_var: _current_second_measure;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _current_amount;;
				-> rrel_3: rrel_fixed: rrel_scp_var: _current_converted_number_link;;
				-> rrel_4: rrel_fixed: rrel_scp_var: _answer;;
			*);;
			-> rrel_3: rrel_assign: rrel_scp_var: _descr;;

			=> nrel_goto: .proc_convert_measure_to_main_measure_call_proc_generate_answer_construction_output_wait;;
		*);;

		// Ждём возврата результата из подпрограммы
		-> .proc_convert_measure_to_main_measure_call_proc_generate_answer_construction_output_wait (*
			<- waitReturn;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

			=> nrel_goto: .proc_convert_measure_to_main_measure_return;;
		*);;

        -> .proc_convert_measure_to_main_measure_print_exception_without_conversion_rate (*
			<- printNl;;
			-> rrel_1: rrel_fixed: rrel_scp_const: [This measures isn't connecting by conversion rate!];;

			=> nrel_goto: .proc_convert_measure_to_main_measure_return;;
		*);;

		-> .proc_convert_measure_to_main_measure_return (*
			<- return;;
		*);;

	*);;
*);;